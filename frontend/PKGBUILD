# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

# Based on: http://www.archlinux.org/packages/community/x86_64/grafana/
# Excluded PhantomJS library due to lack of prebuilt binaries for ARM architectures.
# You may try http://github.com/grafana/grafana-image-renderer plugin instead.

pkgname=grafana-frontend
_pkgname=grafana
pkgver=6.6.1
pkgrel=1
pkgdesc='Gorgeous metric viz, dashboards & editors for Graphite, InfluxDB & OpenTSDB - Frontend'
url='https://grafana.com/'
arch=('any')
license=('Apache')
depends=()
makedepends=('yarn' 'python2' 'nodejs-lts-erbium')
options=(!strip)
source=("$_pkgname-$pkgver.tar.gz::https://github.com/grafana/grafana/archive/v$pkgver.tar.gz"
	'0001-Build-without-PhantomJS.patch')
sha256sums=('efdd6ed9954872eabded9db7cf31b3e7556c13d9b24c7a4751d8ce9b9d5706ae'
            '08abd5f469b970e78414ca64f62c7e00dabb59e3d83b979d711dc3c0a0cde578')

prepare() {
  cd "$_pkgname-$pkgver"
  for f in "${source[@]}"; do
    if [[ "$f" =~ \.patch$ ]]; then
      msg2 "Applying patch ${f##*/}"
      git apply "$srcdir/${f##*/}"
    fi
  done
}

build() {
  msg2 'Cache setup'
  local yarn_cache_folder="$srcdir/tmp/yarn"
  local npm_config_devdir="$srcdir/tmp/gyp"
  local npm_config_prefix="$srcdir/tmp/npm"
  local NODETMP="$srcdir/tmp/node"
  mkdir -p "$yarn_cache_folder"
  mkdir -p "$npm_config_devdir"
  mkdir -p "$npm_config_prefix"
  mkdir -p "$NODETMP"

  msg2 'Building the frontend'
  cd $_pkgname-$pkgver
  env \
    npm_config_devdir="$npm_config_devdir" \
    npm_config_prefix="$npm_config_prefix" \
    YARN_CACHE_FOLDER="$yarn_cache_folder" \
    TMPDIR="$NODETMP" \
    yarn install --pure-lockfile --no-progress --network-concurrency=1 --network-timeout=60000
  env \
    npm_config_devdir="$npm_config_devdir" \
    npm_config_prefix="$npm_config_prefix" \
    TMPDIR="$NODETMP" \
    yarn run build release
}

package() {
  install -dm755 "$pkgdir/usr/share/$pkgname/"
  cd $_pkgname-$pkgver
  for i in public tools; do
    [[ -d "$i" ]] || continue
    cp -r "$i" "$pkgdir/usr/share/$pkgname/$i"
  done
}
