# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

# Based on: http://www.archlinux.org/packages/community/x86_64/grafana/

pkgname=grafana-frontend
_pkgname=grafana
pkgver=7.5.6
pkgrel=1
pkgdesc='Gorgeous metric viz, dashboards & editors for Graphite, InfluxDB & OpenTSDB - Frontend'
url='https://grafana.com/'
arch=('any')
license=('Apache')
depends=()
makedepends=('yarn' 'python' 'nodejs')
options=(!strip)
source=("$_pkgname-$pkgver.tar.gz::https://github.com/grafana/grafana/archive/v$pkgver.tar.gz"
	'0002-Frontend-build-without-source-map.patch'
)
sha256sums=('ea2eeb591e99f7eafb11d117c91509e5feefd2a5448bb9b774445a398f55e9f1'
            '00cfba9ccb1e8cc5f47131455fa6e09260ce1f99f8243116e94330e91da8600d')

prepare() {
  cd "$_pkgname-$pkgver"
  for f in "${source[@]}"; do
    if [[ "$f" =~ \.patch$ ]]; then
      msg2 "Applying patch ${f##*/}"
      git apply "$srcdir/${f##*/}"
    fi
  done
}

build() {
  msg2 'Cache setup'
  local yarn_cache_folder="$srcdir/tmp/yarn"
  local npm_config_devdir="$srcdir/tmp/gyp"
  local npm_config_prefix="$srcdir/tmp/npm"
  local NODETMP="$srcdir/tmp/node"
  mkdir -p "$yarn_cache_folder"
  mkdir -p "$npm_config_devdir"
  mkdir -p "$npm_config_prefix"
  mkdir -p "$NODETMP"

  msg2 'Building the frontend'
  cd $_pkgname-$pkgver
  env \
    npm_config_devdir="$npm_config_devdir" \
    npm_config_prefix="$npm_config_prefix" \
    YARN_CACHE_FOLDER="$yarn_cache_folder" \
    TMPDIR="$NODETMP" \
    yarn install --pure-lockfile --no-progress --network-concurrency=1 --network-timeout=60000
  env \
    npm_config_devdir="$npm_config_devdir" \
    npm_config_prefix="$npm_config_prefix" \
    TMPDIR="$NODETMP" \
    NODE_ENV=production \
    yarn run build
}

package() {
  install -dm755 "$pkgdir/usr/share/$pkgname/"
  cd $_pkgname-$pkgver
  for i in public tools; do
    [[ -d "$i" ]] || continue
    cp -r "$i" "$pkgdir/usr/share/$pkgname/$i"
  done
}
