# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

# Based on: http://www.archlinux.org/packages/community/x86_64/grafana/

pkgname=grafana-frontend
_pkgname=grafana
pkgver=7.5.7
pkgrel=1
pkgdesc='Gorgeous metric viz, dashboards & editors for Graphite, InfluxDB & OpenTSDB - Frontend'
url='https://grafana.com/'
arch=('any')
license=('Apache')
depends=()
makedepends=('yarn' 'python' 'nodejs')
options=(!strip)
source=("$_pkgname-$pkgver.tar.gz::https://github.com/grafana/grafana/archive/v$pkgver.tar.gz"
	'0002-Frontend-build-without-source-map.patch'
	'0003-Update-package.json.patch'
)
sha256sums=('ec1271b5a7202a12cb8046141025ad61aaa42e54425ab40acb7de46461896838'
            '2863bd95f4a7b504ed61d88590b2e435e6c5e300923f6c58171d94e8639f666c'
            'b766c5e7284f502325b24f2acf2472b6f3809083640d55f81f6eafdc4f652d6a')

prepare() {
  cd "$_pkgname-$pkgver"
  for f in "${source[@]}"; do
    if [[ "$f" =~ \.patch$ ]]; then
      msg2 "Applying patch ${f##*/}"
      git apply "$srcdir/${f##*/}"
    fi
  done
}

build() {
  msg2 'Cache setup'
  local yarn_cache_folder="$srcdir/tmp/yarn"
  local npm_config_devdir="$srcdir/tmp/gyp"
  local npm_config_prefix="$srcdir/tmp/npm"
  local NODETMP="$srcdir/tmp/node"
  mkdir -p "$yarn_cache_folder"
  mkdir -p "$npm_config_devdir"
  mkdir -p "$npm_config_prefix"
  mkdir -p "$NODETMP"

  msg2 'Building the frontend'
  cd $_pkgname-$pkgver
  env \
    npm_config_devdir="$npm_config_devdir" \
    npm_config_prefix="$npm_config_prefix" \
    YARN_CACHE_FOLDER="$yarn_cache_folder" \
    TMPDIR="$NODETMP" \
    yarn install --pure-lockfile --no-progress --network-concurrency=1 --network-timeout=60000
  env \
    npm_config_devdir="$npm_config_devdir" \
    npm_config_prefix="$npm_config_prefix" \
    TMPDIR="$NODETMP" \
    NODE_ENV=production \
    yarn run build
}

package() {
  install -dm755 "$pkgdir/usr/share/$pkgname/"
  cd $_pkgname-$pkgver
  for i in public tools; do
    [[ -d "$i" ]] || continue
    cp -r "$i" "$pkgdir/usr/share/$pkgname/$i"
  done
}
